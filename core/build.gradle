plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}


java {
    withJavadocJar()
    withSourcesJar()
}

jar{
    archiveFileName = 'dynamic-api-core.jar'
}

dependencies {
    implementation 'org.springframework:spring-webmvc:5.3.14'
    implementation 'org.springframework:spring-context:5.3.14'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.22.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.2.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'dynamic-api-boot-starter'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Dynamic Api Spring Boot Starter'
                description = '简化Controller层,暴露Service为可访问api'
                url = 'https://github.com/ZhaoRd/dynamic-api.git'
                packaging = 'jar'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ZhaoRd/dynamic-api/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'zhaorende'
                        email = '1260825783@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ZhaoRd/dynamic-api.git'
                    developerConnection = 'scm:git:https://github.com/ZhaoRd/dynamic-api.git'
                    url = 'https://github.com/ZhaoRd/dynamic-api.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}